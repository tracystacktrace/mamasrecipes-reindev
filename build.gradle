buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://cdn.fox2code.com/maven' }
    }
    dependencies {
        classpath('com.fox2code.FoxLoader:dev:2.0-alpha32')
    }
}

apply plugin: 'foxloader.dev'

group = 'net.tracystacktrace'
version = '0.1'

configurations {
    implAndInclude
    implAndInclude.transitive = false
    implementation.extendsFrom(implAndInclude)
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implAndInclude 'com.github.tracystacktrace:mamasrecipes:b05d253f7b'
}

test {
    useJUnitPlatform()
}

jar {
    from(provider {
        configurations.implAndInclude.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }) {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
        exclude '**/package-info.class'
        exclude '**/module-info.class'
        exclude 'assets/*/icon.png'
    }
    from "LICENSE"
}

foxloader {
    modId = "mamasrecipes"
    modName = "Mama\'s Recipes"
    modIcon = "assets/mamasrecipes/icon.png"
    modVersion = project.version
    modAuthors = "tracystacktrace"
    modWebsite = "https://github.com/tracystacktrace/mamasrecipes-reindev"
    modMain = "net.tracystacktrace.mamasrecipes.MamasRecipes"
    modDesc("\u00A7eSupport for custom JSON recipes!")
    modDesc()
    modDesc("\u00A76License: \u00A7aApache License 2.0")
    modDesc("\u00A76License (Core): \u00A7aGNU LGPL v2.1")
    modDesc("\u00A76Sources: \u00A79https://github.com/tracystacktrace/mamasrecipes-reindev")
}
